(define (domain matching-bw-untyped)
(:predicates (block ?b)
	     (hand ?h)
             (hand-positive ?h)
             (hand-negative ?h)
             (block-positive ?b)
             (block-negative ?b)
             (clear ?b)
             (on-table ?b)
             (empty ?h)
             (holding ?h ?b)
             (on ?b1 ?b2)
	     (solid ?b))
(:action pickup
  :parameters (?h ?b)
  :precondition (and (clear ?b) (on-table ?b) (empty ?h) (hand ?h) (block ?b))
  :effect (and (holding ?h ?b) (not (on-table ?b)) (not (clear ?b))
               (not (empty ?h))))
(:action putdown-pos-pos
  :parameters  (?h ?b)
  :precondition (and (holding ?h ?b) (hand-positive ?h) (block-positive ?b) (hand ?h) (block ?b))
  :effect (and (empty ?h) (on-table ?b) (clear ?b)
               (not (holding ?h ?b))))
(:action putdown-neg-neg
  :parameters  (?h ?b)
  :precondition (and  (hand ?h) (block ?b) (holding ?h ?b) (hand-negative ?h) (block-negative ?b))
  :effect (and (empty ?h) (on-table ?b) (clear ?b)
               (not (holding ?h ?b))))
(:action putdown-pos-neg
  :parameters  (?h ?b)
  :precondition (and  (hand ?h) (block ?b) (holding ?h ?b) (hand-positive ?h) (block-negative ?b))
  :effect (and (empty ?h) (on-table ?b) (clear ?b) (not (solid ?b)) 
               (not (holding ?h ?b))))
(:action putdown-neg-pos
  :parameters  (?h ?b)
  :precondition (and  (hand ?h) (block ?b) (holding ?h ?b) (hand-negative ?h) (block-positive ?b))
  :effect (and (empty ?h) (on-table ?b) (not (solid ?b)) (clear ?b)
               (not (holding ?h ?b))))
(:action stack-pos-pos
  :parameters  (?h ?b ?underb)
  :precondition (and (hand ?h) (block ?b) (block ?underb) (clear ?underb) (solid ?underb) (holding ?h ?b) (hand-positive ?h) (block-positive ?b))
  :effect (and (empty ?h) (on ?b ?underb) (clear ?b)
               (not (clear ?underb)) (not (holding ?h ?b))))
(:action stack-neg-neg
  :parameters  (?h ?b ?underb)
  :precondition (and (hand ?h) (block ?b) (block ?underb) (clear ?underb) (solid ?underb) (holding ?h ?b) (hand-negative ?h) (block-negative ?b))
  :effect (and (empty ?h) (on ?b ?underb) (clear ?b)
               (not (clear ?underb)) (not (holding ?h ?b))))
(:action stack-pos-neg
  :parameters  (?h ?b ?underb)
  :precondition (and (hand ?h) (block ?b) (block ?underb) (clear ?underb) (solid ?underb) (holding ?h ?b) (hand-positive ?h) (block-negative ?b))
  :effect (and (empty ?h) (on ?b ?underb) (not (solid ?b)) (clear ?b)
               (not (clear ?underb)) (not (holding ?h ?b))))
(:action stack-neg-pos
  :parameters  (?h ?b ?underb)
  :precondition (and (hand ?h) (block ?b) (block ?underb) (clear ?underb) (solid ?underb) (holding ?h ?b) (hand-negative ?h) (block-positive ?b))
  :effect (and (empty ?h) (on ?b ?underb) (not (solid ?b)) (clear ?b)
               (not (clear ?underb)) (not (holding ?h ?b))))

(:action unstack
  :parameters  (?h ?b ?underb )
  :precondition (and (hand ?h) (block ?b) (block ?underb) (on ?b ?underb) (clear ?b) (empty ?h))
  :effect (and (holding ?h ?b) (clear ?underb) (not (clear ?b))
               (not (on ?b ?underb)) (not (empty ?h)))))
