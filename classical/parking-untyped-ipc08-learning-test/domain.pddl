(define (domain parking-untyped)
 (:predicates 
    (at-curb ?car) 
    (at-curb-num ?car ?curb)
    (behind-car ?car ?front-car)
    (car-clear ?car) 
    (curb-clear ?curb)
    (car ?obj) 
    (curb ?obj) 
 )
	(:action move-curb-to-curb
		:parameters (?car ?curbsrc ?curbdest)
		:precondition (and 
			(curb ?curbsrc)
			(curb ?curbdest)
			(car ?car)
			(car-clear ?car)
			(curb-clear ?curbdest)
			(at-curb-num ?car ?curbsrc)
		)
		:effect (and 
			(not (curb-clear ?curbdest))
			(curb-clear ?curbsrc)
			(at-curb-num ?car ?curbdest)
			(not (at-curb-num ?car ?curbsrc))
		)
	)

	(:action move-curb-to-car
		:parameters (?car ?curbsrc ?cardest)
		:precondition (and 
			(curb ?curbsrc)
			(car ?cardest)
			(car ?car)
			(car-clear ?car)
			(car-clear ?cardest)
			(at-curb-num ?car ?curbsrc)
			(at-curb ?cardest) 
		)
		:effect (and 
			(not (car-clear ?cardest))
			(curb-clear ?curbsrc)
			(behind-car ?car ?cardest)
			(not (at-curb-num ?car ?curbsrc))
			(not (at-curb ?car)) 
		)
	)

	(:action move-car-to-curb
		:parameters (?car ?carsrc ?curbdest)
		:precondition (and 
			(curb ?curbdest)
			(car ?carsrc)
			(car ?car)
			(car-clear ?car)
			(curb-clear ?curbdest)
			(behind-car ?car ?carsrc)
		)
		:effect (and 
			(not (curb-clear ?curbdest))
			(car-clear ?carsrc)
			(at-curb-num ?car ?curbdest)
			(not (behind-car ?car ?carsrc))
			(at-curb ?car) 
		)
	)

	(:action move-car-to-car
		:parameters (?carsrc ?car ?cardest)
		:precondition (and 
			(car ?cardest)
			(car ?carsrc)
			(car ?car)
			(car-clear ?car)
			(car-clear ?cardest)
			(behind-car ?car ?carsrc)
			(at-curb ?cardest) 
		)
		:effect (and 
			(not (car-clear ?cardest))
			(car-clear ?carsrc)
			(behind-car ?car ?cardest)
			(not (behind-car ?car ?carsrc))
		)
	)
)
